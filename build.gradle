plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group = 'com.boy0000'
version = '1.4'

repositories {
    mavenCentral()
    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url = 'https://oss.sonatype.org/content/groups/public/' }
    maven { url = 'https://jitpack.io' }
    // ProtocolLib
    maven { url = 'https://repo.mineinabyss.com/releases/' }
    //maven { url 'https://repo.dmulloy2.net/repository/public/' }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.19.3-R0.1-SNAPSHOT'
    compileOnly 'com.github.LoneDev6:api-itemsadder:3.2.5'
    compileOnly 'com.comphenix.protocol:ProtocolLib:5.0.0-SNAPSHOT'
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = 17
    targetCompatibility = 17
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

boolean copyJar = project.findProperty('copyJar')
String pluginPath = project.findProperty('itemsadder_plugin_path')
if (copyJar && pluginPath != null) {

    task copyJarTask(type: Copy) {
        from 'build/libs/BlockSounds-' + version + '.jar'
        into pluginPath
        doLast {
            println('Copied to plugin directory ' + pluginPath)
        }
    }

    copyJarTask.dependsOn('shadowJar')
    tasks.named('build', DefaultTask).get().dependsOn('copyJarTask')
}
